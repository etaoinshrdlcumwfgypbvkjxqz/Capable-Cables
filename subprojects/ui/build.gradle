//////////////////////////////////////////////////
// TITLE Gradle Build Script
//////////////////////////////////////////////////

import static java.io.File.separator

apply from: "$utilitiesDir${separator}throwable.gradle"
apply from: "$utilitiesDir${separator}io${separator}io.gradle"

apply from: "$modulesDir${separator}optional${separator}generated.gradle"
apply from: "$modulesDir${separator}optional${separator}minecraft-forge.gradle"
apply from: "$modulesDir${separator}optional${separator}jaxb.gradle"
apply from: "$modulesDir${separator}optional${separator}publishing.gradle"

def compileJava = tasks.named 'compileJava'

// COMMENT XJC
File xjcMainComponentsGeneratedDir = file "${getGeneratedDirectory.call 'main'}${separator}xjc_components"
String xjcMainComponentsContextPath = "${jaxbPackagePrefix}.components"
sourceSets.main.java.srcDir xjcMainComponentsGeneratedDir
def xjcMainComponents = createJaxbXJCTask.call('xjcMainComponents',
		sourceSets.main.resources.srcDirs.collectMany { fileTree(it) { include '**/components*.xsd' }.files },
		xjcMainComponentsGeneratedDir,
		['-extension',
		 '-p', xjcMainComponentsContextPath,
		 '-readOnly',
		 '-encoding', 'UTF-8',
		 '-enableIntrospection',
		 '-contentForWildcard',
		 '-xmlschema',
		 '-verbose',
		 '-Xinject-code',
		 '-Xlocator',
		 '-mark-generated', '-Xann', 'javax.annotation.Generated',
		 // COMMENT -Xpropertyaccessors broken
		],
		sourceSets.main.resources.srcDirs.collectMany { fileTree(it) { include '**/components*.xjb' }.files })
compileJava.configure { dependsOn xjcMainComponents }

makeSourcesTasks.call { Task it ->
	it.inputs.properties([
			expand_group                       : project.group,
			expand_buildType                   : buildType,
			expand_xjcMainComponentsContextPath: xjcMainComponentsContextPath,
	])
}

makeResourcesTasks.call { Task it ->
	it.inputs.properties([
			expand_group: project.group,
			expand_modID: modID,
	])
}

makeJUnitPlatformTests.call()

afterEvaluate {
	writePOM()
}
