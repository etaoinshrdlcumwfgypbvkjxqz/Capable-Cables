//////////////////////////////////////////////////
// TITLE Gradle Build Script
//////////////////////////////////////////////////

import groovy.json.JsonBuilder
import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import net.minecraftforge.gradle.user.TaskSourceCopy

import static java.io.File.separator

/* SECTION build script */

buildscript {
    repositories {
        jcenter()
        maven {
            //noinspection SpellCheckingInspection
            name = 'Minecraft Forge'
            url = 'https://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        //noinspection SpellCheckingInspection
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.+'
    }
    // COMMENT Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
}


/* SECTION all projects */

configurations {
    debug {
        canBeResolved false
        canBeConsumed false
    }
    runtimeOnly.extendsFrom debug
}

allprojects {
    /* SECTION extra variables */

    ext {
        utilitiesDir = "$rootDir${separator}gradle"
        modulesDir = "$utilitiesDir${separator}modules"
    }


    /* SECTION application */

    apply from: "$modulesDir${separator}plugins.gradle"

    apply from: "$utilitiesDir${separator}miscellaneous.gradle"
    apply from: "$utilitiesDir${separator}globals.gradle"
    apply from: "$utilitiesDir${separator}collections.gradle"
    apply from: "$utilitiesDir${separator}strings.gradle"
    apply from: "$utilitiesDir${separator}io${separator}io.gradle"
    apply from: "$utilitiesDir${separator}io${separator}properties${separator}properties.gradle"
    apply from: "$utilitiesDir${separator}io${separator}properties${separator}ext.properties.gradle"


    /* SECTION extra variables */

    loadExt "$projectDir${separator}module.properties"


    /* SECTION configuration */

    apply from: "$modulesDir${separator}versioning.gradle"
    apply from: "$modulesDir${separator}information.gradle"

    apply from: "$modulesDir${separator}configurations.gradle"
    apply from: "$modulesDir${separator}repositories.gradle"
    apply from: "$modulesDir${separator}dependencies.gradle"

    apply from: "$modulesDir${separator}source.gradle"
    apply from: "$modulesDir${separator}resources.gradle"
    apply from: "$modulesDir${separator}javadoc.gradle"
    tasks.withType(TaskSourceCopy) { enabled false }

    apply from: "$modulesDir${separator}test.gradle"
    apply from: "$modulesDir${separator}jar.gradle"

    apply from: "$modulesDir${separator}ide.gradle"


    // COMMENT append
    afterEvaluate { storeExt() }
}


apply from: "$utilitiesDir${separator}throwable.gradle"

/* SECTION extra variables */

loadExt "$rootDir${separator}buildSrc${separator}ext-ra.properties", true

ext {
    // COMMENT game

    minecraftVersion = (minecraftForgeVersion as String).split('-').first()
    //noinspection SpellCheckingInspection
    String[] minecraftVersions = (minecraftVersion as String).split('\\.')
    minecraftMajor = minecraftVersions.first().toInteger()
    minecraftMinor = minecraftVersions[1].toInteger()
    minecraftVersionRange = "[$minecraftMajor.$minecraftMinor,$minecraftVersion]".toString()


    // COMMENT mod

    modId = project.name
}

/* SECTION application */

apply from: "$modulesDir${separator}optional${separator}minecraft-forge.gradle"


/* SECTION configuration */

if (hasProperty('lastGoodSettingsJson')) {
    gradle.afterProject {
        Object json = new JsonSlurper().parseText(lastGoodSettingsJson)
        try {
            json.projects?."$path"?.dependencies?.each { if (it != null) dependencies.add it.configuration as String, "$it.group:$it.name:$it.version" as String, { force = true } }
        } catch (Exception e) {
            caughtThrowable e
            logger.info "'$lastGoodSettingsPath' may be corrupted", e
        }
    }
}

task saveCurrentGoodSettings(group: 'build', description: "Saves current good settings to '$lastGoodSettingsPath'.") {
    outputs.file lastGoodSettingsPath

    doLast {
        Wrapper wrapper = tasks.wrapper as Wrapper
        Map<String, Object> json = [
                gradle      : [distributionUrl: wrapper.distributionUrl.replace(wrapper.distributionType.name().toLowerCase(Locale.ROOT), Wrapper.DistributionType.ALL.name().toLowerCase(Locale.ROOT))],
                minecraft   : [
                        version : minecraftForgeVersion,
                        mappings: minecraftMappings
                ],
                projects    : [:]
        ]
        allprojects.each { project ->
            Map<String, Object> jsonProject = json.projects."$project.path" = [
                    dependencies: []
            ]
            configurations.each { configuration ->
                if (configurations.'default'.extendsFrom.contains(configuration)) {
                    if (configuration.canBeResolved) configuration.resolvedConfiguration.firstLevelModuleDependencies.each { jsonProject.dependencies.add([configuration: configuration.name, group: it.moduleGroup, name: it.moduleName, version: it.moduleVersion]) }
                    else configuration.dependencies.each { jsonProject.dependencies.add([configuration: configuration.name, group: it.group, name: it.name, version: it.version]) }
                }
            }
        }
        String jsonText = new JsonBuilder(json).toString()

        createNewFileFromPath(lastGoodSettingsPath).write jsonText
        logger.info('{}:{}{}', convertToCanonicalPath(lastGoodSettingsPath), System.lineSeparator(), JsonOutput.prettyPrint(jsonText))
    }
}
tasks.build.finalizedBy tasks.saveCurrentGoodSettings


//noinspection SpellCheckingInspection
task cleanCacheAutomatically(type: Delete, group: 'build', description: 'Clean old items from cache to reduce space usage.', dependsOn: tasks.saveCurrentGoodSettings) {
    // COMMENT Gradle

    gradle.gradleUserHomeDir.eachDir { it.eachDirMatch(~/[0-9]+[.-][0-9.+-]+/) { if (it.name != gradle.gradleVersion) delete it } }


    // COMMENT Minecraft

    //noinspection SpellCheckingInspection
    String minecraftCachePath = "$gradle.gradleUserHomeDir.absolutePath${separator}caches${separator}minecraft"


    // COMMENT Minecraft Coder Pack
    File mcpDir = file "$minecraftCachePath${separator}de${separator}oceanlabs${separator}mcp${separator}mcp_snapshot"
    if (mcpDir.exists()) mcpDir.eachDir { if ("snapshot_$it.name".toString() != minecraftMappings) delete it }


    // COMMENT Minecraft Forge

    //noinspection SpellCheckingInspection
    File minecraftForgeDir = file "$minecraftCachePath${separator}net${separator}minecraftforge${separator}forge"
    if (minecraftForgeDir.exists()) minecraftForgeDir.eachDir { if (it.name != minecraftForgeVersion) delete it }
}
tasks.saveCurrentGoodSettings.finalizedBy tasks.cleanCacheAutomatically
